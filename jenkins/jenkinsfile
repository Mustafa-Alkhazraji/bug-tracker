pipeline {
    agent any

    environment {
        GOCACHE = "${WORKSPACE}/.go_cache"
        GOMODCACHE = "${WORKSPACE}/.go_mod_cache"
        NPM_CACHE = "${WORKSPACE}/.npm_cache"
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Execute Unit Tests') {
            parallel {
                stage('Unit Tests - Backend') {
                    agent {
                        docker {
                            image 'snakee/golang-junit:1.21'
                            reuseNode false
                        }
                    }
                    steps {
                        withEnv(["GOCACHE=${GOCACHE}", "GOMODCACHE=${GOMODCACHE}"]) {
                            dir('bugtracker-backend') {
                                sh '''
                                    go test ./... -v 2>&1 | go-junit-report > test-results.xml
                                    go test -coverprofile=coverage.out -covermode=atomic ./...
                                    go tool cover -html=coverage.out -o coverage.html
                                    mkdir -p reports
                                    mv coverage.html reports/
                                '''
                            }
                        }
                    }
                    post {
                        always {
                            junit 'bugtracker-backend/test-results.xml'
                            publishHTML target: [
                                reportDir: 'bugtracker-backend/reports',
                                reportFiles: 'coverage.html',
                                reportName: 'Backend Coverage Report'
                            ]
                        }
                    }
                }

                stage('Unit Tests - Frontend') {
                    agent {
                        docker {
                            image 'node:20-alpine'
                            reuseNode false
                        }
                    }
                    steps {
                        dir('bugtracker-frontend') {
                            sh '''
                            npm ci --cache $NPM_CACHE
                            npm test
                            mkdir -p reports
                            mv coverage reports/ || true
                            '''
                        }
                    }
                    post {
                        always {
                            junit 'bugtracker-frontend/test-results.xml'
                            publishHTML target: [
                                reportDir: 'bugtracker-frontend/reports/coverage',
                                reportFiles: 'index.html',
                                reportName: 'Frontend Coverage Report'
                            ]
                            sh 'rm -rf bugtracker-frontend/node_modules || true'
                        }
                    }
                }
            }
        }

        stage('Launch Application') {
            agent {
                docker {
                    image 'docker:24.0.5-dind'
                    args '--privileged -v /var/run/docker.sock:/var/run/docker.sock'
                    reuseNode false
                }
            }
            steps {
                sh 'docker compose up --build -d'
            }
        }

        stage('API Testing') {
            agent {
                docker {
                    image 'mcr.microsoft.com/playwright:v1.50.0-noble'
                    reuseNode false
                    args '--user 0 --network=host'
                }
            }
            steps {
                dir('tests-api') {
                    sh '''
                    npx wait-port http://localhost:8080/api/health -t 30000
                    npm ci --cache $NPM_CACHE
                    npx playwright test
                    '''
                }
            }
            post {
                always {
                    sh 'rm -rf tests-api/node_modules || true'
                    junit 'tests-api/test-results/results.xml'
                    publishHTML target: [
                        reportDir: 'tests-api/playwright-report',
                        reportFiles: 'index.html',
                        reportName: 'Playwright API Test Report'
                    ]
                }
            }
        }
    }

    post {
        always {
            // إيقاف Docker Compose وتنظيف الـ containers
            sh 'docker compose down || true'
            sh 'docker system prune -af || true'

            // تنظيف workspace مع الاحتفاظ بـ cache
            cleanWs(
                deleteDirs: true,
                notFailBuild: true,
                patterns: [
                    [pattern: '.go_cache', type: 'EXCLUDE'],
                    [pattern: '.go_mod_cache', type: 'EXCLUDE'],
                    [pattern: '.npm_cache', type: 'EXCLUDE']
                ]
            )
        }
    }
}
